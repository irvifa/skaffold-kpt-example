plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'com.google.cloud.tools.jib' version '3.1.4'
}

group 'com.github.irvifa'
version '1.0-SNAPSHOT'

sourceCompatibility = '11'
targetCompatibility = '11'

project.ext {
    mainClass = 'com.github.irvifa.helloworld.HelloWorldApplication'
}

repositories {
    jcenter()
    mavenCentral()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.42.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.7.0'
    implementation group: 'io.dropwizard', name: 'dropwizard-json-logging', version: dropwizardVersion
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jib {
    from {
        image = 'gcr.io/distroless/java-debian10:11'
    }
    to {
        image = 'docker.io/irvifa/skaffold-kpt-example'
    }
    container {
        jvmFlags = ['-Xdebug', '-XX:+AlwaysPreTouch',
                    '-Dcom.sun.management.jmxremote.port=9011', '-Dcom.sun.management.jmxremote.rmi.port=9011',
                    '-Dcom.sun.management.jmxremote.ssl=false', '-Dcom.sun.management.jmxremote.authenticate=false',
                    '-Djava.rmi.server.hostname=localhost']
        mainClass = mainClass
        ports = ['8080', '8081']
         format = 'OCI'
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}